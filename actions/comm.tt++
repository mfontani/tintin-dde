#EVENT {IAC SB GMCP Comm.Channel.Text IAC SE}
{
    #variable gmcp[channel] {%0};
    #local text {%0};
    #local {testo} {$text[testo]<g15>};
    #replace testo {\\r} {};
    #replace testo {\\n} {};
    #local {canale} {$text[canale]};
    #local {da_a} {};
    #if {"$text[soggetto]"!=""} {
        #local {da_a} {da $text[soggetto]<g15>}
    };
    #if {"$canale" == "messaggio" || "$canale" == "sogno" || "$canale" == "dicoa" || "$canale" == "rispondi"} {
        #local {da_a} {$da_a a $text[oggetto]<g15>}
    };
    #if {"$canale" == "dico"} {
        #local {cmsg} {<179>}
    };
    #elseif {"$canale" == "gruppo"} {
        #local {cmsg} {<149>}
    };
    #elseif {"$canale" == "immtalk"} {
        #local {cmsg} {<129>}
    };
    #elseif {"$canale" == "parla"} {
        #local {cmsg} {<139>}
    };
    #elseif {"$canale" == "urlo" || "$canale" == "grido"} {
        #local {cmsg} {<119>}
    };
    #elseif {"$canale" == "dicoa"} {
        #local {cmsg} {<159>}
    };
    #else {
        #local {cmsg} {<169>}
    };

    #if {"$da_a"==""} {
        #format {msg} {$cmsg%-9s %s} {$canale<g15>:} {$cmsg$testo<g15>};
    } { 
        #format {msg} {$cmsg%-9s %s: %s} {$canale<g15>:} {$da_a} {$cmsg$testo<g15>};
    };
    ...comm_window_show all $msg;
    #if {"$canale" == "messaggio" || "$canale" == "sogno" || "$canale" == "dicoa"}
    {
        ...comm_window_show msg $msg
    };
    #elseif {"$canale" == "gruppo"}
    {
        ...comm_window_show grp $msg
    };
    #else
    {
        ...comm_window_show pub $msg
    };
};
